USE [Inland]
GO
/****** Object:  StoredProcedure [tco].[cliente_editar]    Script Date: 5/12/2018 10:11:15 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROC [tco].[cliente_editar]
	@Empresa_id INT,
	@Usuario_modificacion_id INT,
	@Unidad_negocio_id INT,
	@Documento_identidad VARCHAR(MAX),
	@Tipo_documento INT,
	@Razon_social VARCHAR(MAX),
	@Linea_id INT,
	@Alias VARCHAR(MAX),
	@Activo CHAR(1),
	@estado INT OUT,
	@mensaje varchar(max) OUT 
AS

SET NOCOUNT ON

BEGIN TRY
	IF(SELECT COUNT(*) FROM ges.empresa CLI WHERE CLI.documento=@Documento_identidad AND CLI.tipo_documento=@Tipo_documento AND CLI.unidad_negocio_id=@Unidad_negocio_id AND CLI.empresa_id!=@Empresa_id) = 0 
	BEGIN
		
		if(SELECT COUNT(*) FROM tco.empresa_alias ALI WHERE ALI.alias=@Alias AND ALI.linea_id=@Linea_id AND ALI.empresa_id != @Empresa_id) = 0 
		BEGIN
			-- Actualiza cliente
			UPDATE ges.empresa
			SET
				documento = @Documento_identidad,
				tipo_documento = @Tipo_documento,
				razon_social = @Razon_social,
				estado = @Activo,
				unidad_negocio_id = @Unidad_negocio_id,
				usuario_modificacion_id = @Usuario_modificacion_id,
    			fecha_modificacion = CURRENT_TIMESTAMP
			WHERE
				empresa_id = @Empresa_id

			-- Elimino Alias
			DELETE FROM tco.empresa_alias WHERE empresa_id = @Empresa_id AND linea_id = @Linea_id

			-- Registro Alias
			INSERT INTO 
					tco.empresa_alias(empresa_id, alias, linea_id)
			VALUES
					(@Empresa_id, @Alias, @Linea_id)
	
			SET @estado = 1;
			SET @mensaje = 'Se actualiz√≥ la empresa correctamente';
		END
		ELSE
		BEGIN
			SET @estado = 0;
			SET @mensaje = 'Consignee Code ya se encuentra asignado a otro cliente';
		END
	END
	ELSE
	BEGIN
		SET @estado = 0;
		SET @mensaje = 'Estos datos ya se encuentran registrados en otra empresa';
	END
END TRY
BEGIN CATCH
       
    DECLARE @log_id INT, @el INT = ERROR_LINE(), @en INT = ERROR_NUMBER(), @em NVARCHAR(4000) = ERROR_MESSAGE()
    EXEC seg.log_registrar @Usuario_modificacion_id, 'tco.cliente_editar', @el, @en, @em, @log_id OUTPUT
    SET @mensaje = CAST(@log_id AS VARCHAR)+' | '+ERROR_MESSAGE();
    SET @estado = 0;

END CATCH