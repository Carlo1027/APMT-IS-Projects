USE [Inland]
GO
/****** Object:  StoredProcedure [tco].[dropoff_editar]    Script Date: 3/12/2018 10:24:17 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROC [tco].[dropoff_editar] (
	@Dropoff_id INT,
	@Cliente_id INT,
	@Ciudad_id INT,
	@Linea_id INT,
	@Deposito_id INT,
	@Fecha_inicio DATE,
	@Fecha_fin DATE,
	@Comentario VARCHAR(255),
	@Activo CHAR(1),
	@Condiciones xml,
	@Usuario_id INT,
	@estado INT OUT,
	@mensaje varchar(max) OUT 
)
AS
SET NOCOUNT ON
BEGIN TRY

	-- 1. Editando el dropoff
	UPDATE 
		tco.dropoff
	SET    [empresa_id] = @Cliente_id
		  ,[ciudad_id] = @Ciudad_id
		  ,[linea_id] = @Linea_id
		  ,[deposito_id] = @Deposito_id
		  ,[fecha_inicio] = @Fecha_inicio
		  ,[fecha_fin] = @Fecha_fin
		  ,[comentario] = @Comentario
		  ,[estado] = @Activo
		  ,[fecha_modificacion] = CURRENT_TIMESTAMP
		  ,[usuario_modificacion_id] = @Usuario_id
	 WHERE 
		dropoff_id = @Dropoff_id

	-- 2. Borrando las condiciones
	DELETE FROM [tco].[condicion] WHERE dropoff_id = @Dropoff_id

	-- 3. Registrando las condiciones (Reglas)
	IF(@Condiciones IS NOT NULL)
	BEGIN		
		INSERT INTO [tco].[condicion] ([dropoff_id], [contenedor_tipo], [contenedor_tamano], [indicador_ilimitado], [cupo], [indicador_dropoff], [dropoff], [tipo_moneda])
		SELECT @Dropoff_id,
			C.value('contenedor_tipo[1]','int') AS contenedor_tipo,
			C.value('contenedor_tamano[1]','int') AS contenedor_tamano,
			C.value('indicador_ilimitado[1]','char(1)') AS indicador_ilimitado,
			CASE 
			  WHEN C.value('cupo[1]','int') != ''  THEN C.value('cupo[1]','int')
			  ELSE null
			END AS cupo,
			C.value('indicador_dropoff[1]','char(1)') AS indicador_dropoff,
			CASE 
			  WHEN C.value('dropoff[1]','int') != ''  THEN C.value('dropoff[1]','int')
			  ELSE null
			END AS dropoff,
			C.value('tipo_moneda[1]','int') AS tipo_moneda
		FROM @Condiciones.nodes('/Regla') AS T(C)
	END

	SET @estado = 1
	SET @mensaje = 'Se edit√≥ el Drop Off correctamente'
		
END TRY
BEGIN CATCH
	
	DECLARE @log_id INT, @el INT = ERROR_LINE(), @en INT = ERROR_NUMBER(), @em NVARCHAR(4000) = ERROR_MESSAGE()
	EXEC seg.log_registrar @Usuario_id, 'tco.dropoff_editar', @el, @en, @em, @log_id OUTPUT
	SET @mensaje = CAST(@log_id AS VARCHAR)+' | '+ERROR_MESSAGE();
	SET @estado = 0;

END CATCH